buildscript {
  repositories {
      jcenter()
  }
  dependencies {
      classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
  }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'license'
apply plugin: 'checkstyle'

group = 'org.poweredrails'
version = '0.1-DEVELOPMENT'
ext.name = 'Rails'
ext.url = 'http://poweredrails.org'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

repositories {
  mavenCentral()
  maven {
    name = 'sonatype-nexus'
    url = 'https://oss.sonatype.org/content/groups/public/'
  }
}

dependencies {
  testCompile 'junit:junit:4.12'
  testCompile 'org.hamcrest:hamcrest-library:1.3'
  testCompile 'org.mockito:mockito-core:1.10.19'

  compile 'org.apache.logging.log4j:log4j-api:2.3'
  compile 'org.apache.logging.log4j:log4j-core:2.3'
  compile 'com.google.guava:guava:18.0'

  checkstyle 'com.puppycrawl.tools:checkstyle:6.10.1'

}

configure([compileJava, compileTestJava]) {
    options.compilerArgs += ['-Xlint:all', '-Xlint:-path']
    options.deprecation = true
    options.encoding = 'UTF-8'
}

jar {
  manifest {
    attributes(
                'Built-By': System.properties['user.name'],
                'Created-By': "${System.properties['java.vm.version']} (${System.properties['java.vm.vendor']})",
                'Implementation-Description': name,
                'Implementation-Version': version,
                'Implementation-Vendor': url
    )
  }
}

task setSourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task setJavadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives setSourceJar
    archives setJavadocJar
}

task getWrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

license {
    header project.file('HEADER.txt')
    include '**/*.java'

    ignoreFailures false
    strictCheck true

    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

processResources {
    from 'LICENSE.txt'
}

checkstyle {
    configFile = file('checkstyle.xml')
    configProperties = [
            'basedir': project.projectDir,
            'suppressions': project.file('checkstyle-suppressions.xml'),
            'severity': 'warning'
    ]
}